locals {
  domain     = "consul"
  namespace  = "consul"
  datacenter = "dc1"
}

module "eks_consul_installer" {
  source           = "../eks/modules/terraform-aws-eks-consul"
  create_namespace = true
  chart_namespace  = local.namespace

  # global
  consul_datacenter = local.datacenter
  consul_domain     = local.domain

  # global.gossipEncryption
  gossip_enable_auto_generate = true

  # global.tls
  tls_enable_auto_encrypt = true

  # client
  client_enable = true

  # global.metrics
  metrics_enabled        = true
  enable_agent_metrics   = true
  enable_gateway_metrics = true

  # connectInject 
  enable_connect_inject                 = true
  connect_inject_by_default             = false
  connect_inject_default_enable_merging = false

  # acl
  manage_system_acls = false
}


resource "kubernetes_deployment_v1" "frontend" {
  metadata {
    name = "frontend"

    labels = {
      app = "frontend"
    }
  }

  spec {
    replicas = 1

    selector {
      match_labels = {
        app = "frontend"
      }
    }

    template {
      metadata {
        labels = {
          app = "frontend"
        }

        annotations = {
          #"consul.hashicorp.com/connect-inject" = "true"
        }
      }

      spec {
        container {
          name  = "frontend"
          image = "ghcr.io/consul-up/birdwatcher-frontend:1.0.0"

          port {
            container_port = 6060
          }

          env {
            name  = "BIND_ADDR"
            value = "0.0.0.0:6060"
          }

          env {
            name  = "BACKEND_URL"
            value = "http://backend" #"http://localhost:7000"
          }
        }
      }
    }
  }
  depends_on = [
    module.eks_consul_installer
  ]
}

resource "kubernetes_service_v1" "frontend" {
  metadata {
    name = "frontend"
    labels = {
      app = "frontend"
    }
    annotations = {
      "service.beta.kubernetes.io/aws-load-balancer-type" = "nlb" 
      "external-dns.alpha.kubernetes.io/hostname" = "front.idtplateer.com"
    }    
  }

  spec {
    type = "LoadBalancer"

    port {
      name        = "http"
      port        = 6060
      target_port = 6060
    }

    selector = {
      app = kubernetes_deployment_v1.frontend.spec[0].selector[0].match_labels.app
    }

  }
  depends_on = [
    module.eks_consul_installer
  ]
}



resource "kubernetes_deployment_v1" "backend" {
  metadata {
    name = "backend"

    labels = {
      app = "backend"
    }
  }

  spec {
    replicas = 1

    selector {
      match_labels = {
        app = "backend"
      }
    }

    template {
      metadata {
        labels = {
          app = "backend"
        }
        annotations = {
          #"consul.hashicorp.com/connect-inject" = "true"
        }
      }

      spec {
        container {
          name  = "backend"
          image = "ghcr.io/consul-up/birdwatcher-backend:1.0.0"

          port {
            container_port = 7000
          }

          env {
            name  = "BIND_ADDR"
            value = "0.0.0.0:7000"
          }
        }
      }
    }
  }
  depends_on = [
    module.eks_consul_installer
  ]
}

resource "kubernetes_service_v1" "backend" {
  metadata {
    name = "backend"
    labels = {
      app = "backend"
    }
  }

  spec { 
    port {
      protocol    = "TCP"
      port        = 80
      target_port = 7000
    }

    selector = {
      app = kubernetes_deployment_v1.backend.spec[0].selector[0].match_labels.app
    }
  }
  depends_on = [
    module.eks_consul_installer
  ]
}

# Phase 0 > Consul 설치, App 배포 
#
#   terraform apply --auto-approve
#
#   http://front.idtplateer.com:6060/ >> 접근 가능
#
# Phase 1 > "consul.hashicorp.com/connect-inject" = "true" 주석 해제
#
#   terraform apply --auto-approve
#
#   http://front.idtplateer.com:6060/ >> 접근 불가 >> SideCar Proxy 에서 인증되지 않은 Traffic 을 막음
#
#   EKS 에서 Envoy 삽입 확인
#
#   Consul UI 에서 Service 등록 확인
#      kubectl port-forward service/consul-server --namespace consul 8501:8501
#      https://localhost:8501/ui/dc1/services
#
#   App 배포확인  
#      kubectl get deployment,service --selector app=frontend
#      kubectl get deployment,service --selector app=backend
#
#   App 접근확인
#      kubectl exec consul-server-0 -n consul -- curl -sS http://frontend.default:6060 >> ERROR 
#      kubectl exec deploy/frontend -c frontend -- curl -si http://backend/bird >> SUCCESS 
# 
#   terraform destroy --auto-approve
